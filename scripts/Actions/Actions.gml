/////////////////////////
//// ATTACK ACTIONS ////
///////////////////////

//This is where all action card attacks go

/* HOW TO
 * All functions should look like this
 * function action(_targetEnemy)
*/



///@self oAttackCard
function actionHeal(_targetEnemy)
{
	//Buff Value
	var _value = value*1.2;
	
    //Juice
    cardJuice(false);
    effectHealth(x,y,10,true,10,25,sprite_get_width(sCardBlank),sprite_get_height(sCardBlank));
    effectNumber(x,y,_value);
    
    //Give Health
    if _targetEnemy
    {
        healPlayer(_value);
    } else healEnemy(_value);
    
    //Play Voice
    audioPlayVoice([voiceInfo.healAnymore,voiceInfo.healMid],false,3);
}


///@self oAttackCard
function actionShield(_targetEnemy)
{
	//Buff Value
	var _value = value * 1.2;
	
    //Juice
    cardShake();
    
    //Effect
    instance_create_layer(x,y,"Attacks",oShield,{
        value : _value,
        card : self.id
    });
    
    //Give Temp Health
    addEffect(attackEffects.shield,_value,!_targetEnemy);
    
    //Play Voice
    audioPlayVoice(voiceInfo.shield,false,3);
}


///@self oAttackCard
function actionSlash(_targetEnemy)
{
    //Juice
    cardShake();
    
    //Create Sword
    instance_create_layer(x,y,"Attacks",oSlashAttack,{
        targetEnemy : _targetEnemy,
        value : value,
        card : self.id
    });
    
}


///@self oAttackCard
function actionBread(_targetEnemy)
{
    //Create Bread
    instance_create_layer(x,y,"Attacks",oBreadAttack,{
        targetEnemy : _targetEnemy,
        value : value,
        breadId : 1,
        card : self.id
    });
}


///@self oAttackCard
function actionLaser(_targetEnemy)
{
    instance_create_layer(x,y,"Attacks",oLaserAttack,{
        targetEnemy : _targetEnemy,
        value : value,
        card : self.id,
    });
}


///@self oAttackCard
function actionPunch(_targetEnemy)
{
    instance_create_layer(x,y,"Attacks",oFistAttack,{
        targetEnemy : _targetEnemy,
        value : value,
        card : self.id,
    });
}


///@self oAttackCard
function actionPoison(_targetEnemy)
{
    //Juice
    cardJuice(false);
    
    //Apply Poison
    addEffect(attackEffects.poison,value*.7,_targetEnemy);
    
    //Play Voice
    audioPlayVoice([voiceInfo.poisonInfect,voiceInfo.poisonShip,voiceInfo.poisonSizz],false,3);
}


///@self oAttackCard
function actionFlameAce(_targetEnemy)
{
	//Juice
	cardJuice(false);
	
    //Create Ace
    instance_create_layer(x,y,"Attacks",oFlameAceAttack,{
        targetEnemy : _targetEnemy,
        value : value,
        card : self.id
    });
}


///@self oAttackCard
function actionMirror(_targetEnemy)
{
	//Create Mirror
    instance_create_layer(x,y,"Attacks",oMirror,{
        value : value,
        card : self.id,
        targetEnemy : _targetEnemy
    });
}


///@self oAttackCard
function actionGhost(_targetEnemy)
{
	//Juice
	cardJuice(false);
	
    //Create Ghost
    instance_create_layer(x,y,"Attacks",oGhostAttack,{
        targetEnemy : _targetEnemy,
        value : value,
        card : self.id
    });
}


///@self oAttackCard
function actionTaunt(_targetEnemy)
{
    var _y = 0;
    if _targetEnemy
    {
        _y = bbox_top - 50;
    } else _y = bbox_bottom + 50;
    
    instance_create_layer(x,_y,"Attacks",oTaunt,{
        targetEnemy : _targetEnemy,
        value : value,
        card : self.id
    });
}


///@self oAttackCard
function actionSteak(_targetEnemy)
{	
    //Juice
    cardJuice(false);
    effectHealth(x,y,10,true,10,25,sprite_get_width(sCardBlank),sprite_get_height(sCardBlank));
    effectNumber(x,y,value);
    
    //Increase Max Health
    if _targetEnemy
    {
        global.playerMaxHp += value;
    } else global.enemyMaxHp += value;
}


///@self oAttackCard
function actionSilence(_targetEnemy)
{
    //No Hit
    if OG_prng_roll(global.SilenceRng) //irandom_range(2,20) < min(value,20)
    {
        effectTextSprite(x,_targetEnemy ? bbox_top : bbox_bottom,sNope);
        cardShake();
        exit;
    }
    
    //Juice
    cardJuice(true,false,true);
    cardFlash(.5,c_white);
    
    //Neglect Target Card
    with oAttackCard
    {
        if cardId == other.cardId and targetEnemy != _targetEnemy
        {
            shake = 1;
            targetScale = .9;
            disabled = true;
        }
    }
    
    //Destroy Attacks
    var _list = ds_list_create();
    if collision_rectangle_list(x-10,0,x+10,room_height,oParAttacks,false,true,_list,true)
    {
        //Star
        effectStar(ds_list_find_value(_list,0).x,ds_list_find_value(_list,0).y,5,false);
        
        //Destroy
        instance_destroy(ds_list_find_value(_list,0));
    }
    ds_list_destroy(_list);
    
}


///@self oAttackCard
function actionJerryCan(_targetEnemy)
{
	//Get Effects
    var _didDamage = false;
    with oAttackEffect
    {
        if info == attackEffects.fire
        {
            //Set Did Damage
            _didDamage = true;
            
            //Get Value
            var _value = value * (other.value*2);
            
            //Hurt Target
            if _targetEnemy
            {
                hurtEnemy(_value);
            } else hurtPlayer(_value);
            
            //Remove Effect
            alarm[0] = -1;
            alarm[1] = 1;
        }
    }
    
    //Did Damage
    if _didDamage
    {
        //Explode Health Bar
        with oHealthBar
        {
            for (var i = 0; i < 5; i++) {
                var _x = 0;
                var _y = 0;
                
                if target == "Player" and !_targetEnemy
                {
                    _x = (bbox_right-75) - 60*i;
                    _y = y + irandom_range(-15,15);
                    effectExplosion(_x,_y,false);
                }
                
                if target == "Enemy" and _targetEnemy
                {
                    _x = (bbox_left+75) + 60*i;
                    _y = y + irandom_range(-15,15);
                    effectExplosion(_x,_y,false);
                }
                
            }
        }
        
        //Juice
        cardJuice(true);
        
    } else {
    	effectTextSprite(x,_targetEnemy ? bbox_top : bbox_bottom,sNope);
        cardShake();
    }
}


///@self oAttackCard
function actionBodySlam(_targetEnemy)
{
    //Get Value
    var _percentage = value/100;
    var _value = _targetEnemy ? global.playerTempHp*_percentage : global.enemyTempHp*_percentage;
    _value = floor(_value);
    
    //Remove Temp Hp
    reduceAssEffects(_value,!_targetEnemy);
    
    //Hurt Target
    if _targetEnemy
    {
        hurtEnemy(_value);
    } else hurtPlayer(_value);
    
    //Juice
    shake = 5;
}


//Concept

///@self oAttackCard
function actionBurger(_targetEnemy)
{
	//Buff Value
	var _value = value*1.2;

    ////Change Value Based On Uses
    //var _targetArray = _targetEnemy ? "Player ": "Enemy";
    //_value -= value*.3 * checkCardUses(actionCards.burger.name,_targetArray);
	//
    //Juice
    cardJuice(false);
    effectHealth(x,y,10,true,10,25,sprite_get_width(sCardBlank),sprite_get_height(sCardBlank));
    effectNumber(x,y,_value);
    
    //Give Health
    if _value >= 0
    {
        if _targetEnemy
        {
            healPlayer(_value);
        } else healEnemy(_value);
    } else {
        if _targetEnemy
        {
            hurtPlayer(abs(_value));
        } else hurtEnemy(abs(_value));
    }

    
    //Play Voice
    audioPlayVoice([voiceInfo.healAnymore,voiceInfo.healMid],false,3);
}

///@self oAttackCard
function actionReckless(_targetEnemy)
{
    instance_create_layer(x,y,"Attacks",oReckless,{
        targetEnemy : _targetEnemy,
        value : value,
        card : self.id,
    });
}
